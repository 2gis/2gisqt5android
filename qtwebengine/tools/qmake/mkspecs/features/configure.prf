# Load configure.prf from qtbase first
load(configure)
load(functions)

defineTest(runConfigure) {
    webengine_successfully_configured: return(true)
    linux:contains(QT_CONFIG,no-pkg-config) {
        skipBuild("pkg-config is required")
        return(false)
    }
    # Ignore the cached config tests results in case they were not successful
    CONFIG += recheck
    #Override the config.tests path
    QMAKE_CONFIG_TESTS_DIR = $$QTWEBENGINE_ROOT/tools/qmake/config.tests
    CONFIG_TESTS = $$files($$QMAKE_CONFIG_TESTS_DIR/*.pro, true)
    log("Running configure tests$${EOL}")
    for(test, CONFIG_TESTS) {
        test = $$basename(test)
        test ~= s/\\.pro$//
        qtCompileTest($$test)
    }
    # libcap-dev package doesn't ship .pc files on Ubuntu.
    linux:!config_libcap:skipBuild("libcap development package appears to be missing")
    linux:!config_khr:skipBuild("khronos development headers appear to be missing (mesa/libegl1-mesa-dev)")
    contains(QT_CONFIG, xcb) {
        for(package, $$list("libdrm xcomposite xi xrandr")) {
            !packagesExist($$package):skipBuild("Unmet dependency: $$package")
        }
    }
    isEmpty(skipBuildReason):cache(CONFIG, add, $$list(webengine_successfully_configured))
}


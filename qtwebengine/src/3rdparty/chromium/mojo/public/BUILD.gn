# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/mojo.gni")

group("public") {
  # Meta-target, don't link into production code.
  testonly = true
  deps = [
    ":libmojo_sdk",
    ":sdk",
    "//mojo/public/cpp/application:standalone",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/environment:standalone",
    "//mojo/public/cpp/utility",
    "//mojo/public/interfaces/bindings/tests:test_interfaces",
    "//mojo/public/sky",
  ]

  if (is_linux) {
    deps += [
      "//mojo/public/python",
    ]
  }

  if (mojo_use_dart) {
    deps += [
      "//mojo/public/dart",
    ]
  }

  if (is_android) {
    deps += [
      "//mojo/public/java:system",
      "//mojo/public/java:bindings",
    ]
  }
}

group("sdk") {
  deps = [
    "//mojo/public/c/system",
    "//mojo/public/cpp/application:standalone",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/environment:standalone",
    "//mojo/public/cpp/utility",
    "//mojo/public/interfaces/application",
    "//mojo/public/js",
  ]
}

static_library("libmojo_sdk") {
  complete_static_lib = true
  deps = [ ":sdk" ]
}
